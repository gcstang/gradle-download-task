/**
 * Include the gradle-download-task plugin
 */
plugins {
    id 'de.undercouch.download' version '3.1.1'
}

/**
 * Download a single file to a directory using a mirror server
 */
task downloadFile << {
    def mirrors = [
        'http://central.maven-non-existing.org/maven2/org/citationstyles/styles/1.0/styles-1.0.jar',
        'http://central.maven.org/maven2/org/citationstyles/styles/1.0/styles-1.0.jar',
        'http://central.maven-non-existing2.org/maven2/org/citationstyles/styles/1.0/styles-1.0.jar'
    ] as Queue
    while(true) {
        def mirror = mirrors.poll()
        try {
            download {
                src mirror
                dest buildDir
                overwrite true
            }
            break
        } catch (Exception e) {
            if (mirrors.isEmpty()) {
                throw e
            }
            logger.warn("Could not download file. Trying next mirror.")
        }
    }
}

defaultTasks 'downloadFile'
