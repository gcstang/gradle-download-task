/**
 * Include the gradle-download-task plugin
 */
plugins {
    id 'de.undercouch.download' version '3.1.1'
}

/**
 * Import the Download task. This line is optional.
 * You can of course also always use the full qualified name
 * when you specify a task of type 'Download'.
 */
import de.undercouch.gradle.tasks.download.Download

ext {
    downloadToBuildDir = true
}

def getMavenCentralUrl() {
    return "http://central.maven.org/maven2/"
}

def getStylesJar() {
    return "org/citationstyles/styles/1.0/styles-1.0.jar"
}

/**
 * Download a single file to a directory. Use closures for the src and dest
 * property.
 */
task downloadFile(type: Download) {
    src {
        def mavenUrl = getMavenCentralUrl()
        def stylesJar = getStylesJar()
        return mavenUrl + stylesJar
    }
    dest {
        if (downloadToBuildDir) {
            return buildDir
        }
        return File.createTempDir()
    }
    overwrite true
}

defaultTasks 'downloadFile'
